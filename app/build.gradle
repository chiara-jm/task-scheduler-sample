apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools

    defaultConfig {
        applicationId "chiarajm.android.base.sample"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 1
        versionName "1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        getProps("$projectDir.absolutePath/config/app.properties").each { p ->
            buildConfigField 'String', p.key, p.value
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            file('proguard').listFiles().each { proguardFile it }
        }

        debug {
            minifyEnabled false
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "stage"
    productFlavors {
        dev {
            dimension "stage"
            resValue "string", "app_name", "DEV - Sample Todo"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"

            getProps("$projectDir.absolutePath/config/dev.properties").each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }

        staging {
            dimension "stage"
            resValue "string", "app_name", "QA - Sample Todo"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"

            getProps("$projectDir.absolutePath/config/staging.properties").each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }

        production {
            dimension "stage"
            resValue "string", "app_name", "Sample Todo"
            applicationIdSuffix ""
            versionNameSuffix ""

            getProps("$projectDir.absolutePath/config/production.properties").each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':kotlin-extensions')
    implementation project(':base-mvp-vm')

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    //Android libraries
    implementation "com.android.support:support-v4:$supportLibrary"
    implementation "com.android.support:appcompat-v7:$supportLibrary"
    implementation "com.android.support:design:$supportLibrary"
    implementation "com.android.support:recyclerview-v7:$supportLibrary"
    implementation "com.android.support:cardview-v7:$supportLibrary"
    implementation "com.android.support:customtabs:$supportLibrary"
    implementation "com.android.support:exifinterface:$supportLibrary"
    implementation "com.android.support.constraint:constraint-layout:$constraintLibrary"
    implementation "com.google.android:flexbox:$flexbox"

    //Dagger
    implementation project(':base-dagger')
    kapt "com.google.dagger:dagger-android-processor:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"

    // Others
    implementation "com.jakewharton.timber:timber:$timber"

    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi"

    implementation "com.squareup.picasso:picasso:$picasso"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso"
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "org.mockito:mockito-android:$mockito"
    androidTestImplementation "org.mockito:mockito-core:$mockito"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger"

    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation "com.jakewharton.timber:timber:$timber"
    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-core:$mockito"

}